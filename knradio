#!/usr/bin/perl
# vim:ft=perl:

use strict;
use utf8;
use vars qw($VERSION);
my $APP;

sub usage {
  pod2usage(
      msg  => "$APP $VERSION\n",
    verbose => 1,
    exitval => 0,
  );
}

BEGIN {
  $APP     = 'knradio';
  $VERSION = '0.010';

  use Pod::Usage;
  if( (!@ARGV) or ($ARGV[0] =~ m/--?h(?:elp)?\z/) ) {
    usage();
    exit;
  }
}

use Getopt::Long;
use LWP::Simple qw(get);

my $log     = "~/knradio.log";
my $pidfile = '/tmp/knradio.pid';


my $opt_no_daemon;
GetOptions(
  'np|now-playing' => sub { printf "%s\n", np(); },
  'p|play'         => \&play,
  's|stop'         => \&stop,
  'nd|no-daemon'   => \$opt_no_daemon,
  'h|help'         => \&usage,
);



sub np {
  my @data = split(/\n/, get('http://www.knradio.se/latlist/exfile.php'));
  for my $line(@data) {
# the .. in the pattern is for unidentified garbage chars
    if($line =~ m/Spelas just nu:..(.+)/i) {
      return $1;
    }
  }
}

sub play {
  if(!($opt_no_daemon)) {
    daemonize()
  }
# for some reason mplayer can't handle this url. why?
# Parsing playlist http://juice.citrus3.com:2199/tunein/knradio.pls...
# Playlist parsing disabled for security reasons. Ignoring file.
  exec('cvlc', 'http://juice.citrus3.com:2199/tunein/knradio.pls');
}

sub stop {
  exec('killall', 'vlc');
}

sub daemonize {
  use POSIX 'setsid';
  my $pid = fork();
  exit 0 if $pid;
  exit 1 if ! defined $pid;

  setsid();
  $pid = fork();
  exit 1 if ! defined $pid;

  if($pid) {
    waitpid($pid, 0);
    unlink $pidfile;
    exit 0;
  }
  elsif($pid == 0) {
    my $PID;
    open($PID, '>', $pidfile) or die "Cant create pidfile $pidfile: $!";
    print $PID $$;
    close $PID;

    open(STDOUT, '>', $log);
    open(STDERR, '>', '/dev/null');
    open(STDIN,  '>', '/dev/null');
  }
}



=pod

=head1 NAME

knradio - interface for knradio, 92,2 FM

=head1 DESCRIPTION

knradio is a command line interface for the "knradio" radio station.

=head1 FEATURES

=head2 -np, --now-playing

Show what's currently playing on knradio.

=head2 -p, --play

Start playback.

=head2 -s, --stop

Stop playback.

=head1 OPTIONS

  -np, --now-playing     print the currently playing song title
  -p,  --play            start playback
  -s,  --stop            stop playback
  -n,  --no-daemon       don't fork into the background

  -h,   --help            show this help

=head1 AUTHOR

  Magnus japh Woldrich
  CPAN ID: WOLDRICH
  m@japh.se
  http://japh.se

=head1 CONTRIBUTORS

Floff, for finding the np-url in the javascript mess.

=head1 REPORTING BUGS

Report bugs and/or feature requests to <m@japh.se>.

=head1 COPYRIGHT

Copyright 2016 the B<knradio> L</AUTHOR> as listed above.

=head1 LICENSE

This application is free software; you may redistribute it and/or modify it
under the same terms as Perl itself.

=head1 SEE ALSO

L<http://knradio.se>

=cut
